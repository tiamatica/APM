 R←Test_Packages_001(stopFlag batchFlag);⎕TRAP;dir;src;dist;lines;cfg;rc;msg;zipfile;ns
⍝ Pack a single file and load it
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 dir←getTempDir ⍬
 src dist←(⊂dir,'/'),¨'src' 'dist'
 3 ⎕MKDIR src dist

 ⍝ create Package
 lines←':Namespace Test' 'foo←{⍵}' ':EndNamespace'
 (⊂lines)⎕NPUT src,'/Test.apln'
 cfg←⎕NS''
 cfg.group←'Example'
 cfg.name←'PackageB'
 cfg.version←'0.1.0'
 cfg.source←'Test.apln'
 (⊂APM.JSON cfg)⎕NPUT src,'/',APM.CFG_NAME
 rc msg zipfile←APM.Pack src dist  ⍝ now Pack it
 →T.PassesIf 0=rc
 →T.PassesIf 0<≢zipfile

 rc msg cfg←APM.Load'file://',zipfile
 →T.GoToTidyUp 0≠rc
 ns←⍎cfg.nspath
 →T.GoToTidyUp(,⊂'Test')≢ns.⎕NL-⍳10
 →T.GoToTidyUp'ok'≢ns.Test.foo'ok'
 R←T._OK

∆TidyUp:
 ⎕EX⍕ns
 3 ⎕NDELETE dir
⍝Done
