 test_serverless_registry_publish_package←{
     apm flags←⍺ ⍵
     dir←tempDir ⍬
     _←3 ⎕MKDIR⊢src registry←(⊂dir,'/'),¨'src' 'registry'

     ⍝ initialise new serverless registry
     rc msg←apm.CreateRegistry registry
     assert 0=rc:

     ⍝ create a package
     lines←':Namespace Test' 'foo←{⍵}' ':EndNamespace'
     _←(⊂lines)⎕NPUT src,'/Test.apln'
     cfg←⎕NS''
     cfg.group←'Example'
     cfg.name←'Test'
     cfg.version←'0.1.0'
     cfg.source←'Test.apln'
     _←(⊂⎕JSON cfg)⎕NPUT src,'/apl-package.json'

     rc msg←apm.Publish src('file://',registry)  ⍝ now Publish it
     assert 0=rc:

     _←3 ⎕NDELETE dir
     0
 }
