 GetViaFileRegistry←{
⍝ Load PathToUri zipfile
     registry←⍺
     reg_root←UriToPath registry.uri
     g n v←⍵
     rc msg index←GetRegistryIndex reg_root
     0≠rc:rc msg ⍬
     mask←index{
         g n v←⍵
         0=≢g,n,v:0
         0=≢g,v:⍺.name∊⊂n
         0=≢v:⍺.(group name)∊⊂g n
         (⍺.(group name)∊⊂g n)∧∨/v⍷↑⍺.version
     }g n v
     pkgs←mask/index
     0=≢pkgs:1 'Package not found'⍬

     pkg←pkgs⊃⍨⊃⍋'.'(≠⊆⊢)¨pkgs.version
     zippath←reg_root,'/packages/',pkg.group,'/',pkg.name,'/'
     zipfile←zippath,pkg.group,'-',pkg.name,'-v',pkg.version,'.zip'
     registry.alias GetViaFile PathToUri zipfile
 }
