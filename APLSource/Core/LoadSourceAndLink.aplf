 LoadSourceAndLink←{
     ⍝ Load code from dir ⍵
     ⍝ Load dependencies first
     ⍺←0
     tree←⍺
     dir←⍵
     rc msg cfg←ReadConfig dir
     rc≠0:1 msg ⍬

     cfg.nspath←PackageSpacePath cfg
     9=⎕NC cfg.nspath:0 ''cfg
     setRef←{
         name←⍺{0<≢⍺:⍺ ⋄ ⍵}⍵.name
         ⍺⍺⍎name,'←',⍵.(nspath,'.',api)
     }

     rc←{
         node←⍵
         0=≢node.dependencies:0
         ns←⍎node.nspath
         alias←GetAlias¨node.dependencies
         _←alias(ns setRef)¨tree[tree.id⍳DropAlias¨node.dependencies]
         0
     }tree[tree.id⍳⊂PackageFileName cfg]

     src←dir,'/',cfg.source
     msg←⎕SE.Link.Import cfg.nspath src   ⍝ "msg" might be useful when tracing
     0 ''cfg
 }
