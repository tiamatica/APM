 zip←{
⍝ Zips files specified in `⍺` into file `⍵`.\\
⍝ `⍺` must be a two-item vector:
⍝ 1. folder that contains the files.
⍝ 2. list of filenames from that folder.  ( ⍝TODO⍝ : if empty or ⍺ is simple, zip all files in that folder?! Kai)
⍝ `⍵` may or may not have an extension. If it does not `.zip` is added.\\
⍝ Returns a two-element vector:
⍝ 1. rc: 0 for success and 1 otherwise
⍝ 2. msg: '' in case rc is 0 and potentailly a message otherwise
⍝ To be replace by a .NET core solution one day  ⍝TODO⍝
     (root_dir src_files)←⍺
     {22::1 ⋄ 1≠1 ⎕NINFO ⍵}root_dir:'source directory does not exist'Assert 0
     bool←⎕NEXISTS(⊂root_dir,'/'),¨src_files
     ~∧/bool:((⍕+/~bool),' File',((1<+/~bool)/'s'),' do',((1=+/~bool)/'es'),' not exist')Assert 0
     zip_file←⎕NPARTS ⍵
     (3⊃zip_file),←(0=≢3⊃zip_file)/'.zip'
     zip_file←⊃,/zip_file
     (root_dir src_files)←⍺
     tmp←##.(GetTempDir,'/',UUID4'')
     _←(⊂src_files)⎕NPUT tmp
     cd←CD root_dir
     r←⎕SH cd,BIN'zip -r ',zip_file,' . -i@',tmp
     _←3 ⎕NDELETE tmp
     1:shy←0
 }
