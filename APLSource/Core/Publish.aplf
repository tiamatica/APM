 Publish←{
⍝ Publish a package.\\
⍝ 1. Creates a zip file for the package in a Temp folder
⍝ 2. Checks for uniqueness of the given package/version combination.
⍝ 3. Moves the zip file into the Registry
⍝ 4. Updates the Registry index
⍝ `⍵` must be a two-item vector:
⍝ 1. `source` → folder to create package from
⍝ 2. `registry` → registry to publish package to (alias or uri)
⍝ Result:
⍝ 1. `rc`  ← 0 if OK, error otherwise
⍝ 2. `msg` ← empty or additional information
     source registry←⍵
     rc msg cfg←ReadGlobalConfig''
     0≠rc:rc((⊂'Problem reading the global config file:'),⊆msg)
     uri←cfg{
         0=≢⍺.registries:⍵
         (⍺.registries.alias⍳⊂⍵)⊃⍺.registries.uri,⊂⍵
     }registry
     type←'file://'
     type≢(≢type)↑uri:1 'Only file URI supported'
     path←UriToPath uri

     ⍝ Package
     rc msg zipfile←Pack source(GetTempDir)
     0≠rc:rc msg
     rc msg cfg←ReadConfig source
     0≠rc:rc msg
     rc msg ind←GetRegistryIndex path
     0≠rc:rc msg

     ⍝ validate package and ensure uniqueness in registry
     exists←ind{
         0=≢⍺:0
         (⊂⍵)∊⍺.(group name version)
     }cfg.(group name version)

     exists:1 'Version conflict, package versions must be unique'

     tgt←path,'/packages/',cfg.group,'/',cfg.name,'/'
     rc←3 ⎕MKDIR tgt
     rc←tgt ⎕NMOVE zipfile
     pcfg←cfg.⎕NS'group' 'name' 'version' 'dependencies'
⍝     pcfg.dependencies←{
⍝         0∊⍴⍵:⍬
⍝         ⍵.(group,'/',name,'/',version)
⍝     }cfg.dependencies
     rc←(ind,pcfg)PutRegistryIndex path
     0≠rc:rc'Failed updating index'

     0 ''
 }
