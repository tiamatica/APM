 R←Test_Packages_005(stopFlag batchFlag);⎕TRAP;rc;msg;cfg;tidyUp;uri;dir;srcA;srcB;dist;lines;ns;zipfileB;zipfileA
⍝ Load PackageA that depends for development on PackageB
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 dir←getTempDir ⍬
 srcA srcB dist←(⊂dir,'/'),¨'srcA' 'srcB' 'dist'
 3 ⎕MKDIR⊢srcA srcB dist

⍝ create PackageB
 lines←':Namespace Test' 'foo←{⍵}' ':EndNamespace'
 (⊂lines)⎕NPUT srcB,'/Test.apln'
 cfg←⎕NS''
 cfg.group←'Example'
 cfg.name←'PackageB'
 cfg.version←'0.1.0'
 cfg.source←'Test.apln'
 (⊂APM.JSON cfg)⎕NPUT srcB,'/',APM.CFG_NAME
 (rc msg zipfileB)←APM.Pack srcB dist  ⍝ now Pack it
 →T.GoToTidyUp 0≠rc

⍝ create PackageA
 lines←':Namespace Test' 'foo←##.PackageB.Test.foo' ':EndNamespace'
 (⊂lines)⎕NPUT srcA,'/Test.apln'
 cfg←⎕NS''
 cfg.group←'Example'
 cfg.name←'PackageA'
 cfg.version←'0.1.0'
 cfg.source←'Test.apln'
 cfg.devDependencies←,⊂'file://',zipfileB
 (⊂APM.JSON cfg)⎕NPUT srcA,'/',APM.CFG_NAME
 (rc msg zipfileA)←APM.Pack srcA dist  ⍝ now Pack it
 →T.GoToTidyUp 0≠rc

 (rc msg cfg)←APM.Load'file://',zipfileA
 →T.GoToTidyUp 0≠rc
 ns←⍎cfg.nspath
 →T.GoToTidyUp'PackageB' 'Test'≢ns.⎕NL-⍳10
 →T.GoToTidyUp'ok'≢ns.Test.foo'ok'
 R←T._OK

∆TidyUp:
 :Trap 6 ⋄ ⎕EX⍕ns ⋄ :EndTrap
 3 ⎕NDELETE dir
 ⍝Done
