 R←Test_Packages_001(stopFlag batchFlag);⎕TRAP;dir;src;dist;body;cfg;zipfile;ns
⍝ Pack a single file and load it
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 dir←getTempDir ⍬
 (src dist)←(⊂dir,'/'),¨'src' 'dist'
 src,←'/v0.1.0'
 3 ⎕MKDIR src dist

⍝ create Package
 body←':Namespace Test' 'foo←{⍵}' ':EndNamespace'
 (⊂body)⎕NPUT src,'/Test.apln'
 cfg←APM.InitPackageConfig ⍬
 cfg.group←'Example'
 cfg.name←'PackageB'
 cfg.version←'0.1.0'
 cfg.source←'Test.apln'
 (⊂APM.JSON cfg)⎕NPUT src,'/',APM.CFG_NAME
 zipfile←APM.Pack src dist  ⍝ now Pack it
 →T.GoToTidyUp 0≥≢zipfile

 cfg←APM.Load'file://',zipfile
 ns←⍎cfg.nspath
 →T.GoToTidyUp(,⊂'Test')≢ns.⎕NL-⍳10
 →T.GoToTidyUp'ok'≢ns.Test.foo'ok'
 R←T._OK

∆TidyUp:
 :Trap 6 ⋄ ⎕EX⍕ns ⋄ :EndTrap
 3 ⎕NDELETE dir
⍝Done
