 list←{
 ⍝ Returns list of files found in ⍵ as a vector of text vectors.\\
 ⍝ In case ⍺ is "full" the full, formatted table of the "unzip" command is returned.
 ⍝ To be replace by a .NET core solution one day  ⍝TODO⍝
     ⍺←''
     _←'Invalid left argument: must be either empty or "full"'##.Assert(⊂⍺)∊⍬'' 'full'
     r←⎕SH BIN,'unzip -l ',⍵
     tm←↑r
     mask←2</'-'=' ',3⌷↑tm
     tmc←2↓¯1⊖tm⌿⍨~∧/tm∊' -'
     tbl←mask⊂⍤1⊢tmc
     tblc←' '{1↓¯1↓⍺{⍵/⍨~(2⍴⍺)⍷⍵}⍺,⍵,⍺}¨tbl
     hdr←1⌷tblc
     lc nc←hdr⍳'Length' 'Name'
     tblc[;lc]←(⊂'Length'),{2 1⊃⎕VFI ⍵}¨1↓tblc[;lc]
     ⍺≡'full':tblc
     1↓tblc[;nc]
 }
