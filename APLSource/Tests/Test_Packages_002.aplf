 R←Test_Packages_002(stopFlag batchFlag);⎕TRAP;dir;src;dist;body;cfg;zipfile;res;expected;rc
⍝ Pack single file and check the resulting ZIP file
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 dir←getTempDir ⍬
 src dist←(⊂dir,'/'),¨'src' 'dist'
 src,←'/v0.1.0'
 rc←3 ⎕MKDIR src dist
 assert rc∧.=1
 body←':Namespace Test' 'foo←{⍵}' ':EndNamespace'
 (⊂body)⎕NPUT src,'/Test.apln'
 cfg←APM.InitPackageConfig ⍬
 cfg.group←'Example'
 cfg.name←'Test'
 cfg.version←'0.1.0'
 cfg.source←'Test.apln'
 (⊂APM.JSON cfg)⎕NPUT src,'/',APM.CFG_NAME

 zipfile←APM.Pack src dist  ⍝ now Pack it
 expected←dist,'/',cfg.group,'-',cfg.name,'-v',cfg.version,'.zip'
 →T.GoToTidyUp zipfile≢expected

 →T.GoToTidyUp 0=⎕NEXISTS zipfile
 res←APM.ZIP.list zipfile
 →T.GoToTidyUp 2≠≢res               ⍝ Single file package plus its config file
 R←T._OK

∆TidyUp:
 3 ⎕NDELETE zipfile
⍝Done
