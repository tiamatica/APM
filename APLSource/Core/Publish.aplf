 Publish←{
     source registry_uri←⍵
     ⍝ source → folder to create package from.\\
     ⍝ registry_uri → registry to publish package to.\\
     ⍝ Result:
     ⍝ 1. rc  ← 0 if OK, error otherwise
     ⍝ 2. msg ← result

     type←'file://'
     type≢(≢type)↑registry_uri:1 'Only file URI supported'
     path←UriToPath registry_uri

     ⍝ Package
     rc msg zipfile←Pack source(GetTempDir)
     0≠rc:rc zipfile
     rc msg cfg←ReadConfig source
     0≠rc:rc msg
     rc msg ind←GetRegistryIndex path
     0≠rc:rc msg

     ⍝ validate package and ensure uniqueness in registry
     exists←ind{
         0=≢⍺:0
         (⊂⍵)∊⍺.(group name version)
     }cfg.(group name version)

     exists:1 'Version conflict, package versions must be unique'

     tgt←path,'/packages/',cfg.group,'/',cfg.name,'/'
     rc←3 ⎕MKDIR tgt
     rc←tgt ⎕NMOVE zipfile
     pcfg←cfg.⎕NS'group' 'name' 'version' 'dependencies'
⍝     pcfg.dependencies←{
⍝         0∊⍴⍵:⍬
⍝         ⍵.(group,'/',name,'/',version)
⍝     }cfg.dependencies
     rc←(ind,pcfg)PutRegistryIndex path
     0≠rc:rc'Failed updating index'

     0 ''
 }
