 test_load_single_package←{
     apm flags←⍺ ⍵
     dir←tempDir ⍬
     _←3 ⎕MKDIR⊢src dist←(⊂dir,'/'),¨'src' 'dist'

     ⍝ create Package
     lines←':Namespace Test' 'foo←{⍵}' ':EndNamespace'
     _←(⊂lines)⎕NPUT src,'/Test.apln'
     cfg←⎕NS''
     cfg.group←'Example'
     cfg.name←'PackageB'
     cfg.version←'0.1.0'
     cfg.source←'Test.apln'
     _←(⊂⎕JSON cfg)⎕NPUT src,'/apl-package.json'
     rc zipfile←apm.Pack src dist  ⍝ now Pack it
     assert 0=rc:

     rc cfg←apm.Load'file://',zipfile
     assert 0=rc:
     ns←⍎cfg.nspath
     assert(,⊂'Test')≡ns.⎕NL-⍳10:
     assert'ok'≡ns.Test.foo'ok':
     _←⎕EX⍕ns
     _←3 ⎕NDELETE dir
     0
 }
