 R←Test_Zip_002(stopFlag batchFlag);⎕TRAP;ZIP;sourceDir;targetDir;targetFile;rc;msg;list
⍝ Zip a directory with three files in it and then list the contents.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←∆Failed
 ZIP←##.Core.ZIP

 sourceDir←getTempDir ⍬
 targetDir←getTempDir'Myzip'
 targetFile←targetDir,'/MyZipFile'
 (⊂'This test file 1')⎕NPUT(sourceDir,'/test1.txt')1
 (⊂'This test file 2')⎕NPUT(sourceDir,'/test2.txt')1
 (⊂'This test file 3')⎕NPUT(sourceDir,'/test3.txt')1
 list←{⊃,/1↓⎕NPARTS ⍵}¨⊃0 ⎕NINFO⍠('Wildcard' 1)⊣sourceDir,'/*'
 rc msg←(sourceDir list)ZIP.zip targetFile
 →GoToTidyUp 0≠rc
 →GoToTidyUp 0≠≢msg

 list←ZIP.list targetFile
 →GoToTidyUp 3≠≢list

 list←'full'ZIP.list targetFile
 →GoToTidyUp 0=≢list
 →GoToTidyUp 2≠≡list

 R←∆OK

∆TidyUp:
 3 ⎕NDELETE¨sourceDir targetFile

⍝Done
