 R←Test_Zip_001(stopFlag batchFlag);⎕TRAP;ZIP;sourceDir;targetDir;targetFile;rc;msg;list
⍝ Zip a directory with three files in it and then unzip it again.
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed
 ZIP←##.Core.ZIP

 sourceDir←getTempDir ⍬
 targetDir←getTempDir'Myzip'
 targetFile←targetDir,'/MyZipFile'
 (⊂'This test file 1')⎕NPUT(sourceDir,'/test1.txt')1
 (⊂'This test file 2')⎕NPUT(sourceDir,'/test2.txt')1
 (⊂'This test file 3')⎕NPUT(sourceDir,'/test3.txt')1
 list←{⊃,/1↓⎕NPARTS ⍵}¨⊃0 ⎕NINFO⍠('Wildcard' 1)⊣sourceDir,'/*'
 rc msg←(sourceDir list)ZIP.zip targetFile
 →T.GoToTidyUp 0≠rc
 →T.GoToTidyUp 0≠≢msg
 3 ⎕NDELETE sourceDir
 3 ⎕MKDIR sourceDir
 list←⊃0 ⎕NINFO⍠('Wildcard' 1)⊣sourceDir,'/*'
 →T.GoToTidyUp 0≠≢list
 assert 1=1 ⎕NINFO sourceDir
 rc←sourceDir ZIP.unzip targetFile
 list←⊃0 ⎕NINFO⍠('Wildcard' 1)⊣sourceDir,'/*'
 →T.GoToTidyUp 3≠≢list

 R←T._OK

∆TidyUp:
 3 ⎕NDELETE¨sourceDir targetFile
⍝Done
