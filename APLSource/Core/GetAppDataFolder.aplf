 GetAppDataFolder←{
⍝ Returns standard path for any application-specific data.\\
⍝ Works on all platforms but returns different results.\\
⍝ `⍵`, if not empty, is added to the resulting path.\\
⍝ Under Windows:\\
⍝ `C:\Users\<<⎕AN>>\AppData\Local/apm'      ←→ GetAppDataFolder ''
⍝ `C:\Users\<<⎕AN>>\AppData\Local/apm/foo'  ←→ GetAppDataFolder 'foo'
     add←{((0≠≢⍵)/'/'),⍵}⍵
     p v←2↑'.'⎕WG'APLVersion'
     w l←'WL'∊''⍴p
     d←'/apm'
     h lad←2 ⎕NQ'.' 'GetEnvironment'('HOME' 'LOCALAPPDATA')
     w:lad,d,add            ⍝ windows

     as←'/Library/Application Support'  ⍝ special mac folder
     l∧0≠≢h:h,d,add         ⍝ linux
     0≠≢h:h,as,d,add        ⍝ mac

     u←⊃~∘∊⍨2 ⎕NQ'.' 'GetEnvironment'('LOGNAME' 'USER' 'LNAME' 'USERNAME')
     0=≢u:d                 ⍝ ? what if no user found  ⍝TODO⍝
     l:'/home/',u,d,add     ⍝ linux
     '/Users/',u,as,d,add   ⍝ mac
 }
