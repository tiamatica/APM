 R←Test_Packages_001(stopFlag batchFlag);⎕TRAP;dir;src;dist;lines;cfg;rc;msg;zipfile;ns
⍝ Pack a single file
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←∆Failed

 dir←getTempDir ⍬
 src dist←(⊂dir,'/'),¨'src' 'dist'
 3 ⎕MKDIR src dist

 ⍝ create Package
 lines←':Namespace Test' 'foo←{⍵}' ':EndNamespace'
 (⊂lines)⎕NPUT src,'/Test.apln'
 cfg←⎕NS''
 cfg.group←'Example'
 cfg.name←'PackageB'
 cfg.version←'0.1.0'
 cfg.source←'Test.apln'
 (⊂⎕JSON cfg)⎕NPUT src,'/apl-package.json'
 rc msg zipfile←APM.Pack src dist  ⍝ now Pack it
 →PassesIf 0=rc
 →PassesIf 0<≢zipfile

 rc msg cfg←APM.Load'file://',zipfile
 →GoToTidyUp 0≠rc
 ns←⍎cfg.nspath
 →GoToTidyUp(,⊂'Test')≢ns.⎕NL-⍳10
 →GoToTidyUp'ok'≢ns.Test.foo'ok'
 R←∆OK

∆TidyUp:
 ⎕EX⍕ns
 3 ⎕NDELETE dir
⍝Done
