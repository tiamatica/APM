 Publish←{
     source registry_uri←⍵
     ⍝ source → folder to create package from
     ⍝ registry_uri → registry to publish package to
     ⍝ rc  ← 0 if OK, error otherwise
     ⍝ msg ← result

     'file://'≢7↑registry_uri:1 'Only file scheme supported'
     path←UriToPath registry_uri

     ⍝ Package
     0≠⊃rc zipfile←Pack source(GetTempDir):rc zipfile
     0≠⊃rc cfg←ReadConfig source:rc cfg
     0≠⊃rc ind←GetRegistryIndex path:rc ind

     ⍝ validate package and ensure uniqueness in registry
     exists←ind{
         0∊⍴⍺:0
         (⊂⍵)∊⍺.(group name version)
     }cfg.(group name version)

     exists:1 'Version conflict, package versions must be unique'

     tgt←path,'/packages/',cfg.group,'/',cfg.name,'/'
     rc←3 ⎕MKDIR tgt
     rc←tgt ⎕NMOVE zipfile
     pcfg←cfg.⎕NS'group' 'name' 'version' 'dependencies'
⍝     pcfg.dependencies←{
⍝         0∊⍴⍵:⍬
⍝         ⍵.(group,'/',name,'/',version)
⍝     }cfg.dependencies
     0≠rc←(ind,pcfg)PutRegistryIndex path:rc'Failed updating index'

     0
 }
