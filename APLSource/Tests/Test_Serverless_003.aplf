 R←Test_Serverless_003(stopFlag batchFlag);⎕TRAP;dir;src;registry;rc;msg;body;cfg
⍝ Publish package
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')
 R←T._Failed

 dir←getTempDir ⍬
 src registry←(⊂dir,'/'),¨'src' 'registry'
 3 ⎕MKDIR src registry

 ⍝ initialise new serverless registry
 rc msg←APM.CreateRegistry registry
 →T.GoToTidyUp 0≠rc

 ⍝ create a package
 body←':Namespace Test' 'foo←{⍵}' ':EndNamespace'
 (⊂body)⎕NPUT src,'/Test.apln'
 cfg←⎕NS''
 cfg.group←'Example'
 cfg.name←'Test'
 cfg.version←'0.1.0'
 cfg.source←'Test.apln'
 (⊂APM.JSON cfg)⎕NPUT src,'/',APM.CFG_NAME

 rc msg←APM.Publish src('file://',registry)  ⍝ now Publish it
 →T.GoToTidyUp 0≠rc
 R←T._OK

∆TidyUp:
 3 ⎕NDELETE dir
⍝Done
