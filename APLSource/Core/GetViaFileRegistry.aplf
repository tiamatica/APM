 GetViaFileRegistry←{
⍝ Load PathToUri zipfile
     registry←⍺
     reg_root←UriToPath registry.uri
     (g n v)←⍵
     index←GetRegistryIndex reg_root
     mask←index{
         (g n v)←⍵
         0=≢g,n,v:0
         0=≢g,v:⍺.name∊⊂n
         0=≢v:⍺.(group name)∊⊂g n
         (⍺.(group name)∊⊂g n)∧⊣/v⍷↑⍺.version
     }g n v
     pkgs←mask/index
     _←'Package not found'Assert 0≠≢pkgs
     vc←3↑2⊃'.'⎕VFI v
     vs←{3↑2⊃'.'⎕VFI ⍵}¨pkgs.version
     mask2←(,⊂vc)⍸vs
     _←'Package not found'Assert∨/mask2
     pkg←(⊃⍒mask2/vs)⊃mask2/pkgs
     zippath←reg_root,'/packages/',pkg.group,'/',pkg.name,'/'
     zipfile←zippath,pkg.group,'-',pkg.name,'-v',pkg.version,'.zip'
     registry.alias GetViaFile PathToUri zipfile
 }
